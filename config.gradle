/*
 * Copyright 2015 Chaos
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// config for properties
Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties');
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}
def umengKey = properties.getProperty('UMENG_APPKEY')
ext.UMENG_APPKEY = umengKey != null ? umengKey : ""

// config for dependencies
ext {
    libsVersion = [support : '23.2.1',
                   retrofit: '2.0.0']

    libs = [supportAnnotations: "com.android.support:support-annotations:${libsVersion.support}",
            recyclerviewV7    : "com.android.support:recyclerview-v7:${libsVersion.support}",
            cardviewV7        : "com.android.support:cardview-v7:${libsVersion.support}",
            appcompatV7       : "com.android.support:appcompat-v7:${libsVersion.support}",
            design            : "com.android.support:design:${libsVersion.support}",
            supportV4         : "com.android.support:support-v4:${libsVersion.support}"]
}

// config for android config
def versionCode() {
    def cmd = 'git rev-list HEAD --no-merges --count'
    cmd.execute().text.trim().toInteger()
}

def versionName() {  //自动版本名
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "-" + version.substring(matcher.end(), version.length())
    }

    return version
}

// default config for android plugin
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        switch (plugin.class.name) {
            case 'com.android.build.gradle.AppPlugin':
                project.android.applicationVariants.all { variant ->
                    variant.mergedFlavor.versionCode = versionCode()
                    variant.mergedFlavor.versionName = versionName()
                }
            case 'com.android.build.gradle.LibraryPlugin':
                project.android {
                    compileSdkVersion 23
                    buildToolsVersion '23.0.2'

                    defaultConfig {
                        minSdkVersion 14
                        targetSdkVersion 23
                    }
                }
                break
        }
    }
}